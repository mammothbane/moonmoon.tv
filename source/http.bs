function mk_request() as object
    req = CreateObject("roUrlTransfer")

    req.EnableEncodings(true)
    req.RetainBodyOnError(true)

    req.SetCertificatesFile("common:/certs/ca-bundle.crt")
    req.InitClientCertificates()

    return req
end function

function twitch_web_client_id() as string
    return "kimne78kx3ncx6brgo4mv6wki5h1ko"
end function

function twitch_headers() as object
    return {
        "client-id": twitch_web_client_id(),
        "accept": "application/vnd.twitchtv.v5+json"
    }
end function

function apply_headers(req as object, headers as object) as void
    for each key in headers
        val = headers[key]
        req.AddHeader(key, val)
    end for
end function

function get_json(url as String, headers = {} as object) as dynamic
    req = mk_request()

    req.SetUrl(url.EncodeUri())
    apply_headers(req, headers)

    resp = req.GetToString()

    return ParseJson(resp)
end function
