function mkRequest() as dynamic
    req = CreateObject("roUrlTransfer")

    req.EnableEncodings(true)
    req.RetainBodyOnError(true)

    req.SetCertificatesFile("common:/certs/ca-bundle.crt")
    req.InitClientCertificates()

    return req
end function

function mkTwitchRequest() as dynamic
    req = mkRequest()

    applyTwitchHeaders(req)

    return req
end function

function twitch_web_client_id() as string
    return "kimne78kx3ncx6brgo4mv6wki5h1ko"
end function

function applyTwitchHeaders(req as dynamic) as void
    req.AddHeader("Client-ID", twitch_web_client_id())
    req.AddHeader("Accept", "application/vnd.twitchtv.v5+json")
end function

function getJSON(url, headers = {}) as dynamic
    req = mkTwitchRequest()

    req.SetUrl(url.EncodeUri())

    for each key in headers
        val = headers[key]
        req.AddHeader(key, val)
    end for

    resp = req.GetToString()

    return ParseJson(resp)
end function
